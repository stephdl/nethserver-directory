#!/usr/bin/perl

#
# Copyright (C) 2012 Nethesis S.r.l.
# http://www.nethesis.it - support@nethesis.it
# 
# This script is part of NethServer.
# 
# NethServer is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License,
# or any later version.
# 
# NethServer is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with NethServer.  If not, see <http://www.gnu.org/licenses/>.
#

use NethServer::Directory;
use NethServer::Service;
use Net::LDAP;

#
# Ensure daemons are running
#
foreach my $daemon (qw(slapd nslcd)) {
    if(NethServer::Service::start($daemon) != 0) {
	sleep 1;
    } else {
	die("Could not start `$daemon`.\n");
    }
}


#
# Connect to LDAP server
#
my $ldap = NethServer::Directory->new or die("Cannot connect LDAP server");
my $NslcdUid = getpwnam('nslcd');
my $LdapInternalSuffix = NethServer::Directory::getInternalSuffix(); 
my $LdapDomainSuffix = NethServer::Directory::getDomainSuffix(); 
my $errors = 0;

#
# Configure server certificate file (default openldap-servers location)
#
my $CertificateFile = '/etc/pki/tls/certs/slapd.pem';

#
# Load the rwm overlay, if missing
#
my $RewriterModule = `rpm -ql openldap-servers | grep 'rwm\.la\$'`;
chomp($RewriterModule);

my $olcModuleSearch = $ldap->search(
    base => 'cn=config',
    filter => "(&(cn=module{0})(olcModuleLoad={0}$RewriterModule))",
    attributes => ['olcModuleLoad']
    );

if ($olcModuleSearch->count() == 0) {
    my $ldapAddResponse = $ldap->add(
	'cn=module,cn=config',
	attrs => [
	    cn => 'module',
	    objectClass => 'olcModuleList',
	    olcModuleLoad => $RewriterModule	
	]
    );
    if($ldapAddResponse->is_error) {
	warn "`$RewriterModule` rwm module load error";
	$errors ++;
    }
}

#
# Replace some global settings, access control setup:
#
my @configChangeList = (    
    # FIXME: Updating TLS attributes may result in libuser client
    # connection problems. Here merge() will not touch unchanged
    # attributes:
    ['merge',
     'cn=config',
     attrs => [
	 olcPasswordCryptSaltFormat => '$6$%.86s',
	 olcTLSCipherSuite => 'HIGH:MEDIUM:+SSLv2',
	 olcTLSCACertificateFile => $CertificateFile,
	 olcTLSCertificateFile => $CertificateFile,
	 olcTLSCertificateKeyFile => $CertificateFile,
	 olcTLSVerifyClient => 'never',
	 # dummy value, see delete below:
	 olcTLSCACertificatePath => '/',
    ]],

    # Refs #1491. Remove attribute, if set.  On CentOS-6.3 nss cert DB
    # is used by default: fall back to our pem certificate:
    ['modify',
     'cn=config',
     delete => [
	 olcTLSCACertificatePath
     ]],
    
    ['modify',
     'olcDatabase={-1}frontend,cn=config',
     replace => [
	 olcPasswordHash => '{CRYPT}'
     ]],

    ['modify',
     'olcDatabase={2}bdb,cn=config',
     replace => [
         olcSuffix => $LdapInternalSuffix,
     ]]
    );


foreach(@configChangeList) {
    my @args = @{$_};
    my $method = shift @args;
    my $mergeResult = $ldap->$method(@args);
    if($mergeResult->is_error()) {
	warn 'Error modifying ' . $_->[1] . "\n";
	$errors ++;
    }
}

#
# Configure default ACLs:
#

$ldap->enforceAccessDirective(
    join(' ',
	 qq(by dn.exact="cn=pam,$LdapInternalSuffix" peername.path="/var/run/ldapi" write),
	 qq(by dn.exact="cn=libuser,$LdapInternalSuffix" peername.ip="127.0.0.1" write),
	 qq(by users ssf=71 read),
	 qq(by * none)),
    '*'
    );

$ldap->enforceAccessDirective(
    join(' ',    
	 qq(by dn.exact="cn=pam,$LdapInternalSuffix" peername.path="/var/run/ldapi" write),
	 qq(by dn.exact="cn=libuser,$LdapInternalSuffix" peername.ip="127.0.0.1" write),
	 qq(by anonymous auth),
	 qq(by self write),
	 qq(by * none)), 
    'userPassword'
    );

#
# Enable relay overlay 
# see man slapd-relay, man slapo-rwm
#
my $olcRelaySearch = $ldap->search(
    base => 'cn=config',
    scope => 'one',
    filter => '(olcDatabase={0}relay)',
    attributes => ['olcSuffix'],
    sizelimit => 1
    );

if($olcRelaySearch->count() == 0) {
    my $ldapAddRelayResponse = $ldap->add(
	'olcDatabase=relay,cn=config',
	attrs => [
	    objectClass => ['olcDatabaseConfig', 'olcRelayConfig', 'olcConfig'],
	    olcDatabase => 'relay',
	    olcSuffix => $LdapDomainSuffix,
	    olcAccess => [
		"to attrs=userPassword by anonymous ssf=71 auth by self ssf=71 read", 
		"to * by users read"
	    ]
	]);
    if($ldapAddRelayResponse->is_error) {	
	warn 'Error adding "relay" backend database: ' . $ldapAddRelayResponse->error;
	$errors ++;
    } else {
	addOverlayRwm();
    }
} 


#
# Add the rewriter module to relay backend
#
sub addOverlayRwm
{

    my $olcRelaySearch = $ldap->search(
	base => 'cn=config',
	scope => 'one',
	filter => '(olcDatabase={0}relay)',
	attributes => ['olcSuffix'],
	sizelimit => 1
	);


    if($olcRelaySearch->count() == 0) {
	warn 'Cannot find the relay database';
	return
    }

    #
    # Search for existing rwm overlay configurations:
    #
    my $relayDnSuffix = $olcRelaySearch->entry(0)->dn();
    my $olcOverlaySearch = $ldap->search(
	base => $relayDnSuffix,
	scope => 'one',
	filter => 'objectClass=olcRwmConfig',
	sizelimit => 1
	);

    if($olcOverlaySearch->count() == 0) {

	my $ldapAddOverlayResponse = $ldap->add(
	    'olcOverlay=rwm,' . $relayDnSuffix,
	    attrs => [
		objectClass => ['olcOverlayConfig', 'olcRwmConfig'],
		olcOverlay => 'rwm',
		olcRwmRewrite => 'rwm-suffixmassage "dc=directory,dc=nh"'
		]
	    );
	if($ldapAddOverlayResponse->is_error) {
	    warn "Error adding rewrite module overlay on `$relayDnSuffix`: " . 
		$ldapAddOverlayResponse;
	    $errors ++;
	}

    }
}


NethServer::Service::manage_add('nethserver-directory',
				'slapd',
				'nslcd');

exit($errors == 0 ? 0 : 1);
